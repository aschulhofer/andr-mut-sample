apply plugin: "at.woodstick.pimutdroid"

pimut {
    pitest {
        failWhenNoMutations = true
        exportLineCoverage = true
        outputFormats = ["XML", "HTML"]
        timeoutConstInMillis = 15000

//        targetClasses = ["at.woodstick.mysampleapplication.*"]
//        targetTests = ["at.woodstick.mysampleapplication.*"]

        mutators = [
            "INCREMENTS",
            "VOID_METHOD_CALLS",
            "RETURN_VALS",
            "MATH",
            "NEGATE_CONDITIONALS",
            "INVERT_NEGS",
            "CONDITIONALS_BOUNDARY",
            "REMOVE_CONDITIONALS"
        ]
    }
}

// Fix for robolectric manifest not found on pitest tasks
gradle.taskGraph.whenReady { graph ->
    def pitestTasks = graph.allTasks.findAll { task ->
        task.getName().startsWith("pitest")
    }

    pitestTasks.each { task ->
        def variantMatcher = (task.getName().replace("pitest", "") =~ /([A-Z]?[^A-Z]*)/)

        List<String> variantPaths = []
        while(variantMatcher.find()) {
            variantPaths.add(variantMatcher.group().toLowerCase())
        }
        def variantPath = variantPaths.join("/")
        def targetPath = "${buildDir}/intermediates/sourceFolderJavaResources/test/${variantPath}"

        project.copy {
            from 'src/robolectric/resources/robolectric.properties'
            into targetPath
        }

        println "Add robolectric.properties to $targetPath"
    }
}